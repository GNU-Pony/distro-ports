# Maintainer: Mattias Andr√©e <`base64 -d`(bWFhbmRyZWUK)@member.fsf.org>

pkgname=cerberus
pkgver=1396894918
pkgrel=1
pkgdesc="A compile-time configurable minimal login program that uses libpassphrase"
url="https://github.com/GNU-Pony/cerberus"
arch=(i686 x86_64)
license=(GPL3)
depends=(glibc libpassphrase linux)
makedepends=(make coreutils gcc texinfo gzip pam)
install=cerberus.install
source=("${url}/archive/${pkgver}.tar.gz")
sha256sums=(0e5d321d88e9dee09d5fe49ee441943a668c5d92f0faf44f5cce831f291853ad)

# The dependency on `linux` can be removed if both `OWN_VCS` and `OWN_VCSA` is removed.
# The dependency on `pam` can removed if `am` is change to not be `pam`.
# See `info cerberus configuring` for details on how to configure `cerberus`.
# Works on armv6h (tested on Raspberry Pi) unless `auth=pam`.

build() {
	cd "${srcdir}/cerberus-${pkgver}"
	
	auth=pam # can also be set to `crypt` or `none`
	cpp_defs=(OWN_VCS OWN_VCSA USE_TTY_GROUP)
	path_user=/usr/local/bin:/usr/bin
	path_root=/usr/local/sbin:/usr/local/bin:/usr/bin
	
	cpp_flags=()
	for def in "${cpp_defs[@]}"; do
	    cpp_flags+=( -D"${def}" )
	done
	cpp_flags="EXTRA_CPP_FLAGS=${cpp_flags[*]}"
	
	conf=( AUTH="${auth}" PATH="${path_user}" PATH_ROOT="${path_root}" "${cpp_flags}" )
	
	make PKGNAME="${pkgname}" INSTALL_BIN=/bin "${conf[@]}"
}

package() {
	cd "${srcdir}/cerberus-${pkgver}"
	
	make PKGNAME="${pkgname}" INSTALL_BIN=/bin DESTDIR="${pkgdir}" install
	
	_dir="${pkgdir}/usr/share/licenses/${pkgname}"
	ln -sf -- "/usr/share/licenses/common/GPL3" "${_dir}/LICENSE"
}

