# Maintainer: Mattias Andr√©e <`base64 -d`(bWFhbmRyZWUK)@member.fsf.org>

pkgname=splashtool
pkgver=1414962500
pkgrel=1
pkgdesc="A simple tool for creating SYSLINUX splashes without fuss"
url="https://github.com/GNU-Pony/splashtool"
arch=(any)
license=(AGPL3)
depends=(gzip nafe kbd imagemagick bash awk grep sed coreutils glibc)
makedepends=(make coreutils sed glibc gcc texinfo)
install=splashtool.install
source=("${url}/archive/${pkgver}.tar.gz")
sha256sums=(683cac8ac172b3fa5619307a39f4e27d39b9548f27e45a6dc1db66c98ebed88c)

_prefix=/usr


build() {
	cd "${srcdir}/splashtool-${pkgver}"
	make
}

package() {
	cd "${srcdir}/splashtool-${pkgver}"
	make PREFIX="${_prefix}" PKGNAME="${pkgname}" DESTDIR="${pkgdir}" install
	_dir="${pkgdir}${_prefix}/share/licenses/${pkgname}"
	ln -sf -- "/usr/share/licenses/common/AGPL3" "${_dir}/LICENSE"
}

