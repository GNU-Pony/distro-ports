# Maintainer: Mattias Andr√©e <`base64 -d`(bWFhbmRyZWUK)@member.fsf.org>

pkgname=pony.computer
pkgver=1396832181
pkgrel=1
pkgdesc="Show computer information and a pony"
url="https://github.com/GNU-Pony/pony.computer"
arch=(any)
license=(GPL3)
depends=(sh ponysay coreutils python3)
optdepends=("linux: Additional system information can be provided with Linux kernel's procfs /proc",
            "filesystem: Identification of distribution",
            "lsb-release: Identification of distribution")
makedepends=(make coreutils sed)
source=("${url}/archive/${pkgver}.tar.gz")
sha256sums=(14d4e92d02adaadd9639105f95c2c26a3f7fc5a0c8daa1e46606c97d1046b781)

_prefix=/usr


build() {
	cd "${srcdir}/pony.computer-${pkgver}"
	make PREFIX="${_prefix}" PKGNAME="${pkgname}" DISTRO="Arch Linux" pony.computer
}

package() {
	cd "${srcdir}/pony.computer-${pkgver}"
	make PREFIX="${_prefix}" PKGNAME="${pkgname}" DESTDIR="${pkgdir}" install
	_dir="${pkgdir}${_prefix}/share/licenses/${pkgname}"
	mkdir -p -- "${_dir}"
	ln -sf -- "/usr/share/licenses/common/GPL3" "${_dir}/LICENSE"
}

